# $Id: PKGBUILD 43562 2009-06-25 08:34:37Z giovanni $
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Robson Peixoto

pkgname=unzip-cjk
_pkgname=unzip
pkgver=6.0
pkgrel=4
pkgdesc="Unpacks .zip archives such as those made by PKZIP"
arch=('i686' 'x86_64')
url="http://www.info-zip.org/"
license=('custom')
depends=('bzip2' 'bash')
conflicts=(unzip)
source=(http://downloads.sourceforge.net/infozip/unzip60.tar.gz
        unzip60-alt-iconv-utf8.patch
        unzip60-cjk.patch)
md5sums=('62b490407489521db863b523a7f86375'
         '4dcfb2084e9db5a3f411f470d92ab2a4'
         'd352ff7120b0a76f821800de6637f15d')

build() {
  cd ${srcdir}/${_pkgname}${pkgver/./}
  patch -Np1 -i ${srcdir}/unzip60-alt-iconv-utf8.patch
  patch -Np1 -i ${srcdir}/unzip60-cjk.patch

  # From Debian
  export CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=64 -DACORN_FTYPE_NFS \
  -DWILD_STOP_AT_DIR -DLARGE_FILE_SUPPORT -DUNICODE_SUPPORT \
  -DUNICODE_WCHAR -DUTF8_MAYBE_NATIVE -DNO_LCHMOD -DDATE_FORMAT=DF_YMD \
  -DUSE_BZIP2 -DNATIVE"

  # From Debian
  make -f unix/Makefile LOCAL_UNZIP="$CFLAGS" prefix=/usr LF2="" \
  D_USE_BZ2=-DUSE_BZIP2 L_BZ2=-lbz2 unzips

  make -f unix/Makefile prefix=${pkgdir}/usr INSTALL_PROGRAM="install" \
  install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/unzip/LICENSE

  # Fixing manpages
  cd ${pkgdir}/usr
  mkdir share
  mv man share 
}

